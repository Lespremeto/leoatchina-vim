" This is leoatchina's vim config forked from https://github.com/spf13/spf13-vim
" Sincerely thank him for his great job, and I have made some change according to own requires.
"
"                    __ _ _____              _
"         ___ _ __  / _/ |___ /      __   __(_)_ __ ___
"        / __| '_ \| |_| | |_ \ _____\ \ / /| | '_ ` _ \
"        \__ \ |_) |  _| |___) |_____|\ V / | | | | | | |
"        |___/ .__/|_| |_|____/        \_/  |_|_| |_| |_|
"            |_|
" You can find spf13's origin config  at http://spf13.com
" python support
if has('python3') && executable('python3')
    let g:python_version = 3
    if !WINDOWS()
        silent! python3 1
    endif
elseif has('python') && executable('python')
    let g:python_version = 2
    if !WINDOWS()
        silent! python 1
    endif
else
    let g:python_version = 0
endif
" use vim-plug to install plugs
if (has('job') || g:python_version || has('nvim') || has('lua'))
    " plug install path
    if has('nvim')
        let $PLUG_PATH = '~/.nvim/plug'
    else
        if has("gui_running")
            let $PLUG_PATH = '~/.gvim/plug'
        else
            let $PLUG_PATH = '~/.vim/plug'
        endif
    endif
    set rtp+=$PLUG_PATH
    call plug#begin($PLUG_PATH)
        " complete_engine && complete_snippet
        let g:complete_engine  = "None"
        let g:use_ultisnips = 1
        let g:smart_engine_select = 0
        " Use local config, some tested feature can be add to it
        if filereadable(expand("~/.nvimrc.local")) && has('nvim')
            source ~/.nvimrc.local
        elseif filereadable(expand("~/.gvimrc.local")) && has('gui_running')
            source ~/.gvimrc.local
        elseif filereadable(expand("~/.vimrc.local"))
            source ~/.vimrc.local
        endif
        if WINDOWS()
            Plug 'leoatchina/vim-windows-tools', {'dir':'$HOME\.vim-windows-tools'}
            let $PATH.=";".$HOME."\\.vim-windows-tools"
        endif
        if OSX()
            Plug 'rizzatti/dash.vim'
            Plug 'ybian/smartim'
        endif
        " list only the plugin groups you will use
        if !exists('g:plug_groups')
            let g:plug_groups=['smartcomplete', 'snippet', 'syntax', 'bio']
        endif
        function! HasPlug(plug)
            return count(g:plug_groups, a:plug)
        endfunction
        if HasPlug('shougo') && g:python_version == 3 && v:version >= 800
            let g:use_shougo = 1
        else
            let g:use_shougo = 0
        endif
        " general
        Plug 'scrooloose/nerdtree'
        Plug 'jistr/vim-nerdtree-tabs'
        Plug 'mhinz/vim-startify'
        Plug 'scrooloose/nerdcommenter'
        Plug 'tpope/vim-commentary'
        Plug 'mbbill/undotree'
        Plug 'leoatchina/vim-colorschemes-collections'
        " signs
        if has('signs')
            Plug 'kshenoy/vim-signature'
            Plug 'gorodinskiy/vim-coloresque'
            Plug 'rhysd/conflict-marker.vim'
            Plug 'mhinz/vim-signify'
            if v:version >= 702
                Plug 'nathanaelkane/vim-indent-guides'
            endif
        endif
        " git
        Plug 'tpope/vim-fugitive'
        Plug 'Xuyuanp/nerdtree-git-plugin'
        if v:version < 702
            Plug 'tpope/vim-git'
        endif
        " text
        Plug 'tpope/vim-surround'
        Plug 'tpope/vim-repeat'
        Plug 'tenfyzhong/typecast.vim'
        Plug 'jiangmiao/auto-pairs'
        Plug 'junegunn/vim-journal'
        Plug 'junegunn/vim-easy-align'
        " input method
        if HasPlug('wubi') || HasPlug('pinyin')
            Plug 'leoatchina/ywvim'
        endif
        " biosyntax
        if HasPlug('bio')
            Plug 'bioSyntax/bioSyntax-vim'
        endif
        " markdown
        Plug 'joker1007/vim-markdown-quote-syntax', {'for':'markdown'}
        if has("gui_running") && g:python_version  " gvim for markdown preview
            " voom
            Plug 'vim-voom/VOoM', {'for': 'markdown'}
            Plug 'iamcco/mathjax-support-for-mkdp', {'for':'markdown'}
            Plug 'iamcco/markdown-preview.vim', {'for':'markdown'}
            Plug 'plasticboy/vim-markdown', {'for':'markdown'}
            if v:version >= 704
                Plug 'mzlogin/vim-markdown-toc', {'for':'markdown'}
            else
                Plug 'ajorgensen/vim-markdown-toc', {'for':'markdown'}
            endif
        else
            Plug 'tpope/vim-markdown', {'for':'markdown'}
        endif
        " search/replace/finder
        Plug 'google/vim-searchindex'
        Plug 'easymotion/vim-easymotion'
        " PHP
        if HasPlug('php')
            Plug 'arnaud-lb/vim-php-namespace', {'for' : 'php'}
        endif
        " Javascript
        if HasPlug('javascript')
            Plug 'elzr/vim-json', {'for': 'javascript'}
            Plug 'pangloss/vim-javascript', {'for': 'javascript'}
            Plug 'heavenshell/vim-jsdoc', {'for': 'javascript'}
        endif
        " HTML
        if HasPlug('html')
            Plug 'hail2u/vim-css3-syntax', {'for':['css','htm', 'html', 'xml', 'css3']}
            Plug 'tpope/vim-haml', {'for':['css','htm', 'html', 'xml', 'css3']}
            Plug 'mattn/emmet-vim', {'for':['css','htm', 'html', 'xml', 'css3']}
        endif
        " perl
        if HasPlug('perl')
            Plug 'vim-perl/vim-perl', {'for': 'perl', 'branch': 'dev'}
        endif
        " statuslines
        if has('statusline')
            Plug 'bling/vim-bufferline'
            if v:version >= 702 && HasPlug('airline') || OSX() && has('gui_running')
                Plug 'vim-airline/vim-airline'
            elseif v:version < 702 || !HasPlug('airline')
                Plug 'itchyny/lightline.vim'
                if v:version >= 800 && HasPlug('syntax')
                    if g:use_shougo
                        Plug 'mkalinski/vim-lightline_neomake'
                    else
                        Plug 'maximbaz/lightline-ale'
                    endif
                endif
            endif
        endif
        " ctags
        if v:version >= 703
            if executable('ctags')
                Plug 'majutsushi/tagbar'
            endif
        endif
        " plugins for vim7.4 or later
        if v:version >= 704
            " syntax check and run method
            Plug 'terryma/vim-multiple-cursors'
            Plug 'thinca/vim-quickrun'
            " ctags
            if executable('ctags')
                Plug 'ludovicchabant/vim-gutentags'
            endif
            " search tools
            if v:version >= 800
                if !g:use_shougo
                    Plug 'skywind3000/asyncrun.vim'
                endif
                Plug 'wsdjeg/FlyGrep.vim'
                if (executable('ag') || executable('ack') || executable('rg') || executable('pt'))
                    Plug 'dyng/ctrlsf.vim'
                endif
            else
                Plug 'brooth/far.vim'
            endif
            " sytax
            if HasPlug('syntax')
                if v:version >= 800
                    if g:use_shougo
                        Plug 'neomake/neomake'
                    else
                        Plug 'w0rp/ale'
                    endif
                else
                    Plug 'vim-syntastic/syntastic'
                endif
            endif
            " browser tools & complete tools
            if g:use_shougo
                let g:browser_tool = "denite"
                let g:complete_engine = "deoplete"
            else
                let g:browser_tool = "ctrlp"
                if HasPlug('fzf')
                    if !WINDOWS() || WINDOWS() && executable('fzf')
                        let g:browser_tool = "fzf"
                    endif
                elseif HasPlug('LeaderF') || HasPlug('leaderf')
                    if g:python_version && executable('gcc')
                        let g:browser_tool = "LeaderF"
                    endif
                endif
                " complete_engine
                if HasPlug('youcompleteme') || HasPlug('YCM')
                    if executable('make') && executable('gcc') && g:python_version && !WINDOWS()
                        let g:complete_engine = "YCM"
                    else
                        echoe "Cannot install YouCompleteMe, smart select a complete_engine."
                        let g:smart_engine_select = 1
                    endif
                elseif HasPlug('ncm2')
                    if g:python_version == 3 && v:version >= 800 && has('nvim') && !WINDOWS()
                        let g:complete_engine = "ncm2"
                    else
                        echoe "Cannot install ncm2, smart select a complete_engine."
                        let g:smart_engine_select = 1
                    endif
                elseif HasPlug('deoplete')
                    if g:python_version == 3 && v:version >= 800
                        let g:complete_engine = "deoplete"
                    else
                        echoe "Cannot install deoplete, smart select a complete_engine."
                        let g:smart_engine_select = 1
                    endif
                elseif HasPlug('completor')
                    if g:python_version && v:version >= 800 && !has('nvim')
                        let g:complete_engine = "completor"
                    else
                        echoe "Cannot install completor, smart select a complete_engine."
                        let g:smart_engine_select = 1
                    endif
                elseif HasPlug('asyncomplete')
                    if v:version >= 800
                        let g:complete_engine = "asyncomplete"
                    else
                        echoe "Cannot install asyncomplete, smart select a complete_engine."
                        let g:smart_engine_select = 1
                    endif
                elseif HasPlug('neocomplete')
                    if has('lua')
                        let g:complete_engine = "neocomplete"
                    else
                        echoe "Cannot install neocomplete, smart select a complete_engine."
                        let g:smart_engine_select = 1
                    endif
                elseif HasPlug('neocomplcache')
                    let g:complete_engine = "neocomplcache"
                endif
                " smart select
                if HasPlug('smartcomplete') || g:smart_engine_select
                    " nvim
                    if has('nvim')
                        if v:version >= 800
                            if g:python_version == 3 && !WINDOWS()
                                let g:complete_engine = "ncm2"
                            else
                                let g:complete_engine = "asyncomplete"
                            endif
                        elseif has('lua')
                            let g:complete_engine = "neocomplete"
                        else
                            let g:complete_engine = "neocomplcache"
                        endif
                    " vim8
                    elseif v:version >= 800
                        if g:python_version
                            let g:complete_engine = "completor"
                        else
                            let g:complete_engine = "asyncomplete"
                        endif
                    elseif has('lua')
                        let g:complete_engine = "neocomplete"
                    else
                        let g:complete_engine = "neocomplcache"
                    endif
                endif
            endif
            " install snippets plugins
            if HasPlug('snippet')
                if g:python_version && g:use_ultisnips == 1
                    let g:complete_snippet = "ultisnips"
                    Plug 'SirVer/ultisnips'
                    Plug 'honza/vim-snippets'
                    Plug 'leoatchina/leoatchina-snippets'
                else
                    let g:complete_snippet = "neosnippet"
                    Plug 'Shougo/neosnippet'
                    Plug 'Shougo/neosnippet-snippets'
                endif
            else
                let g:complete_snippet = "None"
            endif
            " install complete plugins
            if g:complete_engine != "None"
                Plug 'Shougo/neco-vim'
            endif
            if g:complete_engine == "YCM"
                if g:python_version == 2
                    Plug 'Valloric/YouCompleteMe',{ 'do' : 'python2 ./install.py --clang-completer' }
                elseif g:python_version == 3
                    Plug 'Valloric/YouCompleteMe',{ 'do' : 'python3 ./install.py --clang-completer' }
                endif
            elseif g:complete_engine == "ncm2"
                Plug 'ncm2/ncm2'
                Plug 'roxma/nvim-yarp'
                " main complete
                Plug 'ncm2/ncm2-bufword'
                Plug 'ncm2/ncm2-path'
                Plug 'ncm2/ncm2-github'
                Plug 'ncm2/ncm2-tagprefix'
                Plug 'filipekiss/ncm2-look.vim'
                Plug 'ncm2/ncm2-vim', {'for':'vim'}
                Plug 'ncm2/ncm2-neoinclude'
                Plug 'Shougo/neoinclude.vim'
                Plug 'Shougo/neco-syntax'
                " language
                Plug 'ncm2/ncm2-pyclang', {'for':['c', 'cpp']}
                if HasPlug('html')
                    Plug 'ncm2/ncm2-cssomni', {'for':['html','css', 'css3']}
                endif
                Plug 'autozimu/LanguageClient-neovim', {
					\ 'branch': 'next',
					\ 'do': 'bash install.sh',
					\ }
                if g:complete_snippet == "ultisnips"
                    Plug 'ncm2/ncm2-ultisnips'
                endif
            elseif g:complete_engine == "deoplete"
                if has('nvim')
                    Plug 'Shougo/deoplete.nvim',  { 'do': ':UpdateRemotePlugins'}
                else
                    Plug 'Shougo/deoplete.nvim'
                    Plug 'roxma/vim-hug-neovim-rpc'
                    Plug 'roxma/nvim-yarp'
                endif
                Plug 'Shougo/neoinclude.vim'
                Plug 'zchee/deoplete-clang', {'for': ['c', 'cpp']}
                Plug 'zchee/deoplete-jedi', {'for': 'python'}
                if HasPlug('go')
                    Plug 'zchee/deoplete-go', {'for': 'go'}
                endif
                if HasPlug('javascript')
                    Plug 'carlitux/deoplete-ternjs'
                endif
            elseif g:complete_engine == "completor"
                Plug 'maralla/completor.vim'
            elseif g:complete_engine == "asyncomplete"
                Plug 'prabirshrestha/asyncomplete.vim'
                Plug 'prabirshrestha/async.vim'
                Plug 'prabirshrestha/vim-lsp'
                Plug 'prabirshrestha/asyncomplete-lsp.vim'
                Plug 'yami-beta/asyncomplete-omni.vim'
                Plug 'prabirshrestha/asyncomplete-buffer.vim'
                Plug 'prabirshrestha/asyncomplete-emoji.vim'
                Plug 'prabirshrestha/asyncomplete-file.vim'
                Plug 'prabirshrestha/asyncomplete-tags.vim'
                Plug 'Shougo/neco-syntax'
                Plug 'prabirshrestha/asyncomplete-necovim.vim'
                Plug 'prabirshrestha/asyncomplete-necosyntax.vim'
                if HasPlug('go')
                    Plug 'prabirshrestha/asyncomplete-gocode.vim'
                endif
                if HasPlug('javascript')
                    Plug 'prabirshrestha/asyncomplete-tscompletejob.vim'
                    Plug 'prabirshrestha/asyncomplete-flow.vim'
                endif
                if HasPlug('rust')
                    Plug 'keremc/asyncomplete-racer.vim'
                endif
                if g:complete_snippet == 'ultisnips'
                    Plug 'prabirshrestha/asyncomplete-ultisnips.vim'
                elseif g:complete_snippet == 'neosnippet'
                    Plug 'prabirshrestha/asyncomplete-neosnippet.vim'
                endif
            elseif g:complete_engine == "neocomplete"
                Plug 'Shougo/neoinclude.vim'
                Plug 'Shougo/neocomplete.vim'
            elseif g:complete_engine == "neocomplcache"
                Plug 'Shougo/neocomplcache.vim'
                if g:complete_snippet == 'ultisnips'
                    Plug 'JazzCore/neocomplcache-ultisnips'
                endif
            endif
            " java
            if HasPlug('java')
                if executable('java') && g:complete_engine == "YCM"
                    Plug 'artur-shaik/vim-javacomplete2', {'for': 'java'}
                    Plug 'dansomething/vim-eclim'
                else
                    echoe "Inroder to install java plugins, you shoud update you system"
                endif
            endif
            " Go
            if HasPlug('go')
                Plug 'fatih/vim-go', {'for': 'go', 'do':':GoUpdateBinaries'}
            endif
            " rust
            if HasPlug('rust')
                Plug 'rust-lang/rust.vim', {'for': 'rust'}
            endif
            " julia
            if HasPlug('julia')
                Plug 'JuliaEditorSupport/julia-vim', {'for': 'julia'}
            endif
        endif
        " install browser tools
        if g:browser_tool == "fzf"
            if !WINDOWS()
                Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
            endif
            Plug 'junegunn/fzf.vim'
        elseif g:browser_tool == "LeaderF"
            if WINDOWS()
                Plug 'Yggdroot/LeaderF', { 'do': '.\install.bat' }
            else
                Plug 'Yggdroot/LeaderF', { 'do': './install.sh' }
            endif
        elseif g:browser_tool == "denite"
            Plug 'Shougo/denite.nvim'
        else
            Plug 'ctrlpvim/ctrlp.vim'
            Plug 'tacahiroy/ctrlp-funky'
        endif
        " after file
        if filereadable(expand("~/.nvimrc.after")) && has('nvim')
            source ~/.nvimrc.after
        elseif filereadable(expand("~/.gvimrc.after")) && has('gui_running')
            source ~/.gvimrc.after
        elseif filereadable(expand("~/.vimrc.after"))
            source ~/.vimrc.after
        endif
    call plug#end()
    command! PlugReinstall PlugClean! | PlugInstall --sync
    command! PlugNew PlugClean! | PlugUpdate --sync
else
    echom "Inorder to install plugins with vim-plug, your vim should be with any of `+job`, `+python`, `+python3`, `+ruby`, or use Neovim"
endif
