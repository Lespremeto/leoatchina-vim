" python support
if has('python3') && executable('python3')
    let g:python_version = 3
    if !WINDOWS()
        silent! python3 1
    endif
    if v:version == 704 && has('patch774') || v:version >= 800 || has('nvim')
        let g:python3_host_prog = expand(exepath('python3'))
    endif
elseif has('python') && executable('python2')
    let g:python_version = 2
    if !WINDOWS()
        silent! python 1
    endif
    if v:version == 704 && has('patch774') || v:version >= 800 || has('nvim')
        let g:python_host_prog = expand(exepath('python2'))
    endif
else
    let g:python_version = 0
endif
" use vim-plug to install plugs
if (has('job') || g:python_version || has('nvim') || has('lua'))
    " plug install path
    if has('nvim')
        let $PLUG_PATH = '~/.nvim/plug'
    elseif has("gui_running")
        if OSX()
            let $PLUG_PATH = '~/.mvim/plug'
        else
            let $PLUG_PATH = '~/.gvim/plug'
        endif
    else
        let $PLUG_PATH = '~/.vim/plug'
    endif
    set rtp+=$PLUG_PATH
    call plug#begin($PLUG_PATH)
        let g:use_complete  = 1
        let g:use_snippet   = 1
        let g:use_syntax    = 1
        let g:use_ultisnips = 1
        let g:use_fuzzyfinder = 1
        " Use local config, some tested feature can be add to it
        if filereadable(expand("~/.nvimrc.local")) && has('nvim')
            source ~/.nvimrc.local
        elseif filereadable(expand("~/.mvimrc.local")) && has('gui_macvim')
            source ~/.mvimrc.local
        elseif filereadable(expand("~/.gvimrc.local")) && has('gui_running')
            source ~/.gvimrc.local
        elseif filereadable(expand("~/.vimrc.local"))
            source ~/.vimrc.local
        endif
        if g:use_complete == 0
            let g:use_syntax = 0
        endif
        if WINDOWS()
            Plug 'pprovost/vim-ps1', { 'for': 'ps1' }
            Plug 'leoatchina/vim-windows-tools', {'dir':'$HOME\.vim-windows-tools'}
            let $PATH.=";".$HOME."\\.vim-windows-tools"
            if has('nvim')
                Plug 'lambdalisue/vim-fullscreen'
            endif
        elseif OSX()
            Plug 'rizzatti/dash.vim'
        elseif LINUX() && has('gui_running')
            Plug 'lambdalisue/vim-fullscreen'
        endif
        " list only the plugin groups you will use
        if !exists('g:plug_groups')
            let g:plug_groups=['leaderf', 'bio']
        endif
        function! HasDirectory(dir)
            return isdirectory(expand($PLUG_PATH."/".a:dir))
        endfunction
        function! HasPlug(plug)
            return count(g:plug_groups, a:plug)
        endfunction
        " general
        Plug 'scrooloose/nerdtree'
        Plug 'jistr/vim-nerdtree-tabs'
        Plug 'mhinz/vim-startify'
        Plug 'scrooloose/nerdcommenter'
        Plug 'mbbill/undotree'
        Plug 'mattn/webapi-vim'
        Plug 'tpope/vim-unimpaired'
        if (v:version < 800 && !has('nvim')) && !WINDOWS()
            Plug 'Shougo/vimproc.vim', {'do' : 'make'}
        endif
        " input method
        if HasPlug('wubi') || HasPlug('pinyin')
            Plug 'leoatchina/ywvim'
        endif
        " signs
        if has('signs')
            Plug 'kshenoy/vim-signature'
            Plug 'gorodinskiy/vim-coloresque'
            Plug 'rhysd/conflict-marker.vim'
            Plug 'mhinz/vim-signify'
        endif
        " indent
        if v:version >= 703 && has('conceal')
            Plug 'Yggdroot/indentLine'
        endif
        " git
        Plug 'tpope/vim-fugitive'
        Plug 'Xuyuanp/nerdtree-git-plugin'
        if v:version < 702
            Plug 'tpope/vim-git'
        endif
        " text
        Plug 'tpope/vim-surround'
        Plug 'tpope/vim-repeat'
        Plug 'tenfyzhong/typecast.vim'
        Plug 'jiangmiao/auto-pairs'
        Plug 'junegunn/vim-journal'
        Plug 'junegunn/vim-easy-align'
        " markdown
        Plug 'joker1007/vim-markdown-quote-syntax', {'for':'markdown'}
        Plug 'hotoo/pangu.vim', {'for':'markdown'}
        if g:python_version
            Plug 'plasticboy/vim-markdown', {'for':'markdown'}
            if has('nvim')
                Plug 'iamcco/markdown-preview.nvim', {'for':'markdown'}
            else
                Plug 'iamcco/markdown-preview.vim', {'for':'markdown'}
            endif
            Plug 'iamcco/mathjax-support-for-mkdp', {'for':'markdown'}
            if v:version >= 704 || has('nvim')
                Plug 'mzlogin/vim-markdown-toc', {'for':'markdown'}
            endif
            " voom
            Plug 'vim-voom/VOoM'
        else
            Plug 'tpope/vim-markdown', {'for':'markdown'}
        endif
        " search/replace/finder
        Plug 'google/vim-searchindex'
        Plug 'easymotion/vim-easymotion'
        Plug 'sbdchd/neoformat'
        if v:version >= 800 || has('nvim')
            Plug 'terryma/vim-multiple-cursors'
            Plug 'wsdjeg/FlyGrep.vim'
            " Ctrlsf
            if executable('ag') || executable('ack') || executable('rg') || executable('pt')
                Plug 'dyng/ctrlsf.vim'
            else
                Plug 'brooth/far.vim'
            endif
        elseif v:version >= 704
            Plug 'terryma/vim-multiple-cursors'
            Plug 'brooth/far.vim'
        endif
        " biosyntax
        if HasPlug('bio')
            Plug 'bioSyntax/bioSyntax-vim'
        endif
        " themes
        Plug 'leoatchina/vim-colorschemes-collections'
        " statuslines
        if has('statusline')
            Plug 'bling/vim-bufferline'
            Plug 'itchyny/lightline.vim'
            if (v:version >= 800 || has('nvim')) && HasPlug('syntax')
                Plug 'maximbaz/lightline-ale'
            endif
        endif
        " debug tools for python c java ternjs .....
        if HasPlug('debug')
            Plug 'sillybun/vim-repl'
            Plug 'idanarye/vim-vebugger'
        endif
        " run tools
        if v:version >= 702
            Plug 'thinca/vim-quickrun'
        endif
        if v:version >= 800 || has('nvim')
            Plug 'skywind3000/asyncrun.vim'
        endif
        " HTML
        if HasPlug('html')
            Plug 'mattn/emmet-vim', {'for':['css','htm', 'html', 'xml', 'css3']}
            Plug 'hail2u/vim-css3-syntax', {'for':['css','htm', 'html', 'xml', 'css3']}
        endif
        " PHP
        if HasPlug('php')
            Plug 'shawncplus/phpcomplete.vim', {'for': 'php'}
        endif
        " javascript
        if HasPlug('javascript')
            Plug 'pangloss/vim-javascript', {'for': 'javascript'}
            Plug 'heavenshell/vim-jsdoc', {'for': 'javascript'}
        endif
        " perl
        if HasPlug('perl')
            Plug 'vim-perl/vim-perl', {'for': 'perl', 'branch': 'dev'}
        endif
        " erlang language
        if HasPlug('erlang')
            Plug 'johnzeng/vim-erlang-omnicomplete', {'for': 'erlang'}
        endif
        " julia language
        if HasPlug('julia') && (v:version >= 704 || has('nvim'))
            Plug 'JuliaEditorSupport/julia-vim', {'for': 'julia'}
        endif
        " java language
        if HasPlug('java') && (v:version >= 704 || has('nvim'))
            Plug 'dansomething/vim-eclim'
        endif
        " tags
        if  (has('patch1058') && v:version == 703 || v:version >= 704 || has('nvim')) && executable('ctags')
            Plug 'majutsushi/tagbar'
        endif
        " gtags
        if executable('gtags') && HasPlug('gtags') && (v:version >= 800 || has('nvim'))
            if exists('$GTAGSCONF')
                Plug 'ludovicchabant/vim-gutentags'
                Plug 'skywind3000/gutentags_plus'
                Plug 'skywind3000/vim-preview'
                Plug 'jsfaint/gen_tags.vim'
            else
                echom("Please set $GTAGSCONF in your coresponding .local file, and pip install pygments.")
            endif
        endif
        " fuzzy_finder
        if g:use_fuzzyfinder
            if !WINDOWS()
                let g:fuzzy_finder = 'fzf '
                Plug 'junegunn/fzf.vim'
                if executable('fzf')
                    Plug 'junegunn/fzf', {'dir': '~/.fzf', 'do': './install --bin'}
                else
                    Plug 'junegunn/fzf', {'dir': '~/.fzf', 'do': './install --all'}
                endif
            else
                let g:fuzzy_finder = ''
            endif
            if (v:version == 704 && has('patch330') || v:version >= 800 || has('nvim')) && g:python_version && !HasPlug('ctrlp')
                if WINDOWS()
                    Plug 'Yggdroot/LeaderF', { 'do': '.\install.bat' }
                else
                    Plug 'Yggdroot/LeaderF', { 'do': './install.sh' }
                endif
                Plug 'Yggdroot/LeaderF-marks'
                let g:fuzzy_finder .= 'LeaderF'
            else
                Plug 'ctrlpvim/ctrlp.vim'
                Plug 'tacahiroy/ctrlp-funky'
                Plug 'fisadev/vim-ctrlp-cmdpalette'
                if g:python_version
                    Plug 'FelikZ/ctrlp-py-matcher'
                else
                    Plug 'raghur/fruzzy', {'do': { -> fruzzy#install()}}
                endif
                let g:fuzzy_finder .= 'ctrlp'
            endif
        else
            let g:fuzzy_finder = "None"
        endif
        " syntax_tools
        if g:use_syntax
            " syntax
            if v:version >= 800 || has('nvim')
                Plug 'w0rp/ale'
                let g:syntax_tool = 'ale'
            elseif v:version == 704 && has('patch1486')
                Plug 'vim-syntastic/syntastic'
                let g:syntax_tool = 'syntastic'
            endif
        else
            let g:syntax_tool = "None"
        endif
        " complete engine
        let g:complete_engine = "None"
        if g:use_complete && (v:version == 704 && has('patch774') || v:version >= 800 || has('nvim')) && has('timers')
            if HasPlug('youcompleteme') || HasPlug('YCM')
                if executable('cmake') && executable('g++') && g:python_version && (has('patch1578') && v:version == 704 || v:version >= 800 || has('nvim'))
                    let g:complete_engine = "YCM"
                else
                    echoe "Cannot install YouCompleteMe, smart select a complete_engine."
                endif
            elseif HasPlug('coc')
                if executable('node') && executable('yarn') && (has('nvim-0.3.0') || v:version >= 801)
                    let g:complete_engine = "coc"
                else
                    echoe "Cannot install coc, smart select a complete_engine."
                endif
            elseif HasPlug('deoplete')
                if g:python_version == 3 && (has('nvim-0.3.0') || v:version>=800)
                   let g:complete_engine = "deoplete"
                else
                    echoe "Cannot install deoplete, smart select a complete_engine."
                endif
            elseif HasPlug('ncm2')
                if g:python_version == 3 && (has('nvim-0.2.2') || v:version >= 800)
                   let g:complete_engine = "ncm2"
                else
                    echoe "Cannot install ncm2, smart select a complete_engine."
                endif
            elseif HasPlug('asyncomplete')
                if has('nvim') || v:version >= 800
                    let g:complete_engine = "asyncomplete"
                else
                    echoe "Cannot install asyncomplete, smart select a complete_engine."
                endif
            elseif HasPlug('neocomplete')
                if has('lua')
                    let g:complete_engine = "neocomplete"
                else
                    echoe "Cannot install neocomplete, smart select a complete_engine."
                endif
            elseif HasPlug('neocomplcache')
                let g:complete_engine = "neocomplcache"
            endif
            " smart select engine
            if g:complete_engine == "None"
                if g:python_version == 3 && has('nvim-0.2.2')
                    let g:complete_engine = "ncm2"
                elseif g:python_version == 3 && v:version >= 800
                    let g:complete_engine = "deoplete"
                elseif has('nvim') || v:version >= 800
                    let g:complete_engine = "asyncomplete"
                elseif has('lua')
                    let g:complete_engine = "neocomplete"
                else
                    let g:complete_engine = "neocomplcache"
                endif
            endif
        else
            let g:complete_engine  = "None"
        endif
        " enginge plus
        let g:complete_snippet = "None"
        if g:complete_engine != "None" && g:use_snippet
            Plug 'Shougo/vimshell.vim'
            Plug 'Shougo/neoinclude.vim'
            Plug 'Shougo/neco-vim'
            Plug 'Shougo/neco-syntax'
            let s:engines = ['YCM', 'deoplete', 'neocomplete']
            if index(s:engines, g:complete_engine) >= 0
                Plug 'tenfyzhong/CompleteParameter.vim'
            endif
            " install snippets pluginstall
            Plug 'leoatchina/leoatchina-snippets'
            if g:python_version && g:use_ultisnips && g:complete_engine != "neocomplcache"
                let g:complete_snippet = "ultisnips"
                Plug 'SirVer/ultisnips'
                Plug 'honza/vim-snippets'
            else
                let g:complete_snippet = "neosnippet"
                Plug 'Shougo/neosnippet'
                Plug 'Shougo/neosnippet-snippets'
            endif
        endif
        if g:complete_engine == "YCM"
            let b:ycm_install = " ./install.py --clang-completer"
            if HasPlug('go') && executable('go')
                let b:ycm_install = b:ycm_install . " --go-completer"
            endif
            if HasPlug('rust') && executable('rust')
                let b:ycm_install = b:ycm_install . " --rust-completer"
            endif
            if HasPlug('java') && executable('java')
                let b:ycm_install = b:ycm_install . " --java-completer"
            endif
            if HasPlug('javascript') && executable('node')
                let b:ycm_install = b:ycm_install . " --ts-completer"
            endif
            if g:python_version == 2
                Plug 'Valloric/YouCompleteMe',{ 'do' : 'python2' . b:ycm_install}
            elseif g:python_version == 3
                Plug 'Valloric/YouCompleteMe',{ 'do' : 'python3' . b:ycm_install}
            endif
        elseif g:complete_engine == "coc"
            " coc not complete python
            function! CocInit()
                let b:coc_install_plugins = [
                    \ 'coc-omni',
                    \ 'coc-tag',
                    \ 'coc-neco',
                    \ 'coc-json',
                    \ 'coc-tslint',
                    \ 'coc-stylelint',
                    \ 'coc-eslint'
                \ ]
                if HasPlug('php') || HasPlug('html')
                    let b:coc_install_plugins = b:coc_install_plugins + [
                        \ 'coc-html',
                        \ 'coc-css',
                        \ 'coc-wxml',
                        \ 'coc-yaml',
                        \ 'coc-emmet'
                \ ]
                endif
                if HasPlug('javascript')
                    let b:coc_install_plugins = b:coc_install_plugins + [
                        \ 'coc-tsserver',
                        \ 'coc-vetur'
                    \ ]
                endif
                if HasPlug('go')
                    execute 'CocInstall coc-gocode'
                endif
                if HasPlug('java')
                    execute 'CocInstall coc-java'
                endif
                if HasPlug('rust')
                    execute 'CocInstall coc-rls'
                endif
                if g:complete_snippet == 'ultisnips'
                    execute 'CocInstall coc-ultisnips'
                elseif g:complete_snippet == 'neosnippet'
                    execute 'CocInstall coc-neosnippet'
                endif
                execute 'CocInstall '. join(b:coc_install_plugins, " ")
            endfunction
            command! -nargs=0 -bang CocInit call CocInit()
            Plug 'neoclide/coc-neco'
            Plug 'neoclide/coc-sources'
            Plug 'neoclide/coc.nvim', {'do': 'yarn install'}
        elseif g:complete_engine == "deoplete"
            if has('nvim')
                Plug 'Shougo/deoplete.nvim', {'branch': 'master', 'do': ':UpdateRemotePlugins'}
            else
                Plug 'Shougo/deoplete.nvim', {'branch': 'master'}
                Plug 'roxma/vim-hug-neovim-rpc'
                Plug 'roxma/nvim-yarp'
            endif
            Plug 'zchee/deoplete-clang'
            Plug 'zchee/deoplete-jedi', {'for': 'python'}
            if HasPlug('javascript') && executable('tern')
                Plug 'carlitux/deoplete-ternjs'
                Plug 'mhartington/nvim-typescript'
            endif
            if HasPlug('rust') && executable('racer')
                Plug 'sebastianmarkow/deoplete-rust'
            endif
            if HasPlug('go') && executable('gocode')
                Plug 'zchee/deoplete-go', { 'do': 'make'}
            endif
            " tabnine now not performance good in linux
            if WINDOWS()
                Plug 'tbodt/deoplete-tabnine', { 'do': 'powershell.exe .\install.ps1' }
            elseif OSX()
                Plug 'tbodt/deoplete-tabnine', { 'do': 'bash ./install.sh' }
            endif
        elseif g:complete_engine == "ncm2"
            Plug 'ncm2/ncm2'
            Plug 'roxma/nvim-yarp'
            if !has('nvim')
                Plug 'roxma/vim-hug-neovim-rpc'
            endif
            " main complete
            Plug 'ncm2/ncm2-bufword'
            Plug 'ncm2/ncm2-path'
            Plug 'ncm2/ncm2-tmux'
            Plug 'ncm2/ncm2-tagprefix'
            Plug 'filipekiss/ncm2-look.vim'
            Plug 'ncm2/ncm2-neoinclude'
            " only use ultisnips
            Plug 'ncm2/ncm2-ultisnips'
            " language
            Plug 'ncm2/ncm2-jedi', {'for':'python'}
            Plug 'ncm2/ncm2-pyclang', {'for':['c', 'cpp']}
            Plug 'ncm2/ncm2-vim', {'for':'vim'}
            if HasPlug('gtags')
                Plug 'ncm2/gtags'
            endif
            if HasPlug('html')
                Plug 'ncm2/ncm2-cssomni', {'for':['css', 'css3']}
            endif
            if HasPlug('javascript') && executable('tern')
                Plug 'ncm2/ncm2-tern', {'for':['javascript', 'typescript']}
            endif
            if HasPlug('rust') && executable('racer')
                Plug 'ncm2/ncm2-racer', {'for':'rust'}
            endif
            if HasPlug('go') && executable('gocode')
                Plug 'ncm2/ncm2-go', {'for':'go'}
            endif
            if HasPlug('java')
                Plug 'ObserverOfTime/ncm2-jc2/', {'for': 'java'}
            endif
            " LCN
            if exists('g:LanguageClient_serverCommands') && has('nvim')
                Plug 'autozimu/LanguageClient-neovim'
            endif
        elseif g:complete_engine == "asyncomplete"
            Plug 'prabirshrestha/asyncomplete.vim'
            Plug 'prabirshrestha/async.vim'
            Plug 'prabirshrestha/vim-lsp'
            Plug 'prabirshrestha/asyncomplete-lsp.vim'
            Plug 'yami-beta/asyncomplete-omni.vim'
            Plug 'prabirshrestha/asyncomplete-buffer.vim'
            Plug 'prabirshrestha/asyncomplete-emoji.vim'
            Plug 'prabirshrestha/asyncomplete-file.vim'
            " vim
            Plug 'prabirshrestha/asyncomplete-necovim.vim'
            Plug 'prabirshrestha/asyncomplete-necosyntax.vim'
            " ctags
            if executable('ctags')
                Plug 'prabirshrestha/asyncomplete-tags.vim'
            endif
            if HasPlug('go')
                Plug 'prabirshrestha/asyncomplete-gocode.vim'
            endif
            if HasPlug('javascript')
                Plug 'prabirshrestha/asyncomplete-flow.vim'
                Plug 'runoshun/tscompletejob'
                Plug 'prabirshrestha/asyncomplete-tscompletejob.vim'
            endif
            if HasPlug('rust') && executable('racer')
                Plug 'keremc/asyncomplete-racer.vim'
            endif
            if g:complete_snippet == 'ultisnips'
                Plug 'prabirshrestha/asyncomplete-ultisnips.vim'
            elseif g:complete_snippet == 'neosnippet'
                Plug 'prabirshrestha/asyncomplete-neosnippet.vim'
            endif
        elseif g:complete_engine == "neocomplete"
            Plug 'Shougo/neocomplete.vim'
            Plug 'davidhalter/jedi-vim'
            Plug 'Rip-Rip/clang_complete'
            if HasPlug('rust') && executable('racer')
                Plug 'racer-rust/vim-racer'
            endif
            if HasPlug('javascript') && executable('tern')
                Plug 'ternjs/tern_for_vim'
                Plug 'Quramy/tsuquyomi'
            endif
        elseif g:complete_engine == "neocomplcache"
            Plug 'Shougo/neocomplcache.vim'
            Plug 'davidhalter/jedi-vim'
            Plug 'Rip-Rip/clang_complete'
        endif
        unlet g:use_ultisnips
        unlet g:use_snippet
        unlet g:use_complete
        unlet g:use_syntax
        unlet g:use_fuzzyfinder
    call plug#end()
    function! GetVersion()
        let l:result=[]
        if has('nvim')
            let v = api_info().version
            call add(l:result,'nvim-')
            call add(l:result,printf('%d.%d.%d', v.major, v.minor, v.patch))
        else
            redir => l:msg
            silent! execute ':version'
            redir END
            if has('gui_macvim')
                call add(l:result, 'mvim-')
            elseif has('gui_running')
                call add(l:result, 'gvim-')
            else
                call add(l:result, 'vim-')
            endif
            call add(l:result,matchstr(l:msg,'VIM - Vi IMproved\s\zs\d.\d\ze'))
            call add(l:result,matchstr(l:msg, ':\s\d-\zs\d\{1,4\}\ze'))
        endif
        return join(l:result, "")
    endfunction
    function! ShowParams()
        let s:params_dict = {
            \ 'python':g:python_version,
            \ 'engine': g:complete_engine,
            \ 'snippet': g:complete_snippet,
            \ 'fuzzyfinder': g:fuzzy_finder,
            \ 'syntax': g:syntax_tool,
            \ 'version' : GetVersion(),
        \ }
        echo string(s:params_dict)
    endfunction
    command! ShowParams call ShowParams()
    command! PlugRe PlugClean! | PlugInstall --sync
    command! PlugNew PlugClean! | PlugUpdate --sync
else
    echom "In order to install plugins with vim-plug, your vim should be with any of `+job`, `+python`, `+python3`, `+ruby`, '+lua', or use Neovim"
endif
